/*
 * <著作権及び免責事項>
 *
 * 　本ソフトウェアはフリーソフトです．自由にご使用下さい．
 *
 * このソフトウェアを使用したことによって生じたすべての障害,損害,不具合等に関しては,
 * 私と私の関係者及び,私の所属する団体とも,一切の責任を負いません．
 * 各自の責任においてご使用下さい
 * 
 * この<著作権及び免責事項>があるソースに関しましては,すべて以下の者が作成しました．
 * 作成者 : mtksum
 * 連絡先 : t-moteki@hykwlab.org
 *
 */


OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

/* 区画と大きさ */
MEMORY
{
	/* DRAM (2MB) */
	dramall(rwx)	: o = 0x400000, l = 0x200000 /* 2MB */
	ram(rwx)			: o = 0x400000 + 0x100, l = 0x200000 - 0x100
	
	/* internal RAM (16KB) */
	ramall(rwx)		: o = 0xffbf20, l = 0x004000 /* 16KB */
	softvec(rw)		: o = 0xffbf20, l = 0x000040 /* top of RAM */
	schdul_area(rw)		: o = 0xffbf60, l = 0x0000a0
	logbuffer(rwx)	: o = 0xffc000, l = 0x001000
	userstack(rw)	: o = 0xffd000, l = 0x002f00
	bootstack(rw)	: o = 0xffff00, l = 0x000000
	intrstack(rw)	: o = 0xffff00, l = 0x000000 /* end of RAM */
}

/* セクション定義 */
SECTIONS
{
	.softvec : {
		_softvec = .;
	} > softvec

	.text : {
		_text_start = . ;
		*(.text)
		_etext = . ;
	} > ram

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = . ;
	} > ram

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > ram

	.bss : {
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > ram

	. = ALIGN(4);
	_end = . ;

/* _endシンボルの下から動的メモリ(ヒープ領域) */
	.freearea : {
		_freearea = .;
	} > ram

	.schdul_area : {
		_schdul_area = .;
	} > schdul_area

	.logbuffer : {
		_logbuffer_start = . ;
		*(.logbuffer)
		. = ALIGN(4); /* アライメント調整 */
		_logbuffer_end = . ;
	} > logbuffer

	.userstack : {
		_userstack = .;
	} > userstack

	.bootstack : {
		_bootstack = .;
	} > bootstack

	.intrstack : {
		_intrstack = .;
	} > intrstack
}
